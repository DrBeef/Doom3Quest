plugins {
    id 'com.android.application'
}

android {
    namespace 'com.drbeef.doom3quest'
    compileSdk 32

    defaultConfig {
        applicationId "com.drbeef.doom3quest"
        minSdk 26
        targetSdk 26

        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        main {
            jniLibs {
                srcDir 'src/main/jni/VrApi/Libs/'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        //If you want to continue even if errors found use following line
        abortOnError false
    }
}

android {
    task PackVrSupportPK4 {
        dependsOn 'Pack399PK4'
        dependsOn 'Pack399RoEPK4'
        dependsOn 'Pack399LMPK4'
        dependsOn 'Pack399CDPK4'
    }
    task Pack399PK4(type: Zip) {
        from 'src/main/pk4/pak399/'
        archiveFileName.set('pak399.pk4')
        getDestinationDirectory().set(file('src/main/assets/'))
    }
    task Pack399RoEPK4(type: Zip) {
        from 'src/main/pk4/pak399roe/'
        archiveFileName.set('pak399roe.pk4')
        getDestinationDirectory().set(file('src/main/assets/'))
    }
    task Pack399LMPK4(type: Zip) {
        from 'src/main/pk4/pak399lm/'
        archiveFileName.set('pak399lm.pk4')
        getDestinationDirectory().set(file('src/main/assets/'))
    }
    task Pack399CDPK4(type: Zip) {
        from 'src/main/pk4/pak399cd/'
        archiveFileName.set('pak399cd.pk4')
        getDestinationDirectory().set(file('src/main/assets/'))
    }
    preBuild.dependsOn PackVrSupportPK4
}
dependencies {
    implementation "com.android.support:support-compat:26.1.0"
    implementation "com.android.support:support-core-utils:26.1.0"
    implementation files('src/main/libs/haptic_service.aar')
}